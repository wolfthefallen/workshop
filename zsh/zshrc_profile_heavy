# $HOME/.zshrc
##################################################
# zsh fully featured profile
# https://github.com/zeroSteiner/workshop
##################################################

# load gabrielelana/awesome-terminal-fonts
source $HOME/.fonts/fontawesome-regular.sh
source $HOME/.fonts/octicons-regular.sh
CODEPOINT_OF_ARROW="e0b0"

function arrow_right {
  echo "\u${CODEPOINT_OF_ARROW}"
}

function git_branch_char {
  GIT_BRANCH_NAME=$(git branch | awk '/^\*/ {print $2}')
  if [[ "$GIT_BRANCH_NAME" == "master" ]]; then
    echo "\u${CODEPOINT_OF_OCTICONS_STAR} "
  elif [[ "$GIT_BRANCH_NAME" == "dev" ]]; then
    echo "\u${CODEPOINT_OF_OCTICONS_GEAR} "
  elif [[ "$GIT_BRANCH_NAME" == "upstream-master" ]]; then
    echo "\u${CODEPOINT_OF_OCTICONS_ROCKET} "
  elif [[ "$GIT_BRANCH_NAME" == pr/* ]]; then
    echo "$GIT_BRANCH_NAME" | sed "s|pr\/|$(echo -ne \\u$CODEPOINT_OF_OCTICONS_GIT_PULL_REQUEST) |"
  else
    echo "$GIT_BRANCH_NAME"
  fi
}

function git_prompt_char {
  if git remote --verbose | grep "origin.\\+gist\.github.com" &> /dev/null; then
    echo "\u${CODEPOINT_OF_OCTICONS_GIST}"
  elif git remote --verbose | grep "origin.\\+github.com" &> /dev/null; then
    echo "\u${CODEPOINT_OF_AWESOME_GITHUB}"
  else
    echo 'g'
  fi
}

function transit_char {
  echo "%{$bg[green]$fg[black]%}$(arrow_right)"
}

autoload -Uz vcs_info
zstyle ':vcs_info:*' enable git hg
zstyle ':vcs_info:*' check-for-changes true
zstyle -e ':vcs_info:git*' formats 'reply=("%{$bg[black]%}%{$fg[white]%} $(git_prompt_char) %u:%c %{$fg_bold[white]%}%r%{$fg_no_bold[white]%}:$(git_branch_char)%{$reset_color%}$(transit_char)")'
zstyle ':vcs_info:git*' stagedstr '+'
zstyle ':vcs_info:git*' unstagedstr '!'
zstyle -e ':vcs_info:hg*' formats 'reply=("%{$bg[black]%}%{$fg[white]%}[h %{$fg_bold[white]%}%r%{$fg_no_bold[white]%}:%b]%{$reset_color%}$(transit_char)")'

precmd() {
  vcs_info
}

setopt prompt_subst
VCSPROMPT='${vcs_info_msg_0_}'
PROMPT="$VCSPROMPT$PROMPT%{$fg[green]%}$(arrow_right)%{$reset_color%}"

[[ -d $HOME/.bin ]] && PATH=$PATH:$HOME/.bin
[[ -f $HOME/.zshrc-local ]] && source $HOME/.zshrc-local
